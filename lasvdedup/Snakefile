#!/usr/bin/env python

from lasvdedup.utils import determine_duplicates

rule all:
    input:
        trees=expand("{OUTDIR}/iqtree-out/lasv-{segment}.denovo.trimal.treefile", segment=config["SEGMENTS"], OUTDIR=config["OUTDIR"]),
        duplicates=expand("{OUTDIR}/dedup/lasv-{segment}.bad_seqs.txt", segment=config["SEGMENTS"], OUTDIR=config["OUTDIR"])

rule prepare_base_files:
    output:
        alignments="{OUTDIR}/raw/lasv-base-{segment}.aln",
        trees="{OUTDIR}/raw/lasv-base-{segment}.treefile"
    params:
        base_dir=config["BASE_DATA_DIR"]
    shell:
        """
        mkdir -p {OUTDIR}/raw
        echo "Processing segment {wildcards.segment} from {params.base_dir}"

        if [[ "{params.base_dir}" == http* ]]; then
            # If BASE_DATA_DIR is a URL, download the file
            echo "Downloading alignment file for {wildcards.segment}..."
            wget -O {output.alignments} "{params.base_dir}/LASV-{wildcards.segment}.aln" || \
            curl -o {output.alignments} "{params.base_dir}/LASV-{wildcards.segment}.aln" || \
            (echo "Failed to download {wildcards.segment} alignment file" && exit 1)
            echo "Alignment download complete."

            echo "Downloading tree file for {wildcards.segment}..."
            wget -O {output.trees} "{params.base_dir}/LASV-{wildcards.segment}.treefile" || \
            curl -o {output.trees} "{params.base_dir}/LASV-{wildcards.segment}.treefile" || \
            (echo "Failed to download {wildcards.segment} tree file" && exit 1)
            echo "Tree download complete."
        else
            # If BASE_DATA_DIR is a local path, copy the file
            cp "{params.base_dir}/LASV-{wildcards.segment}.aln" {output.alignments} || \
            (echo "Failed to copy {wildcards.segment} alignment file" && exit 1)

            cp "{params.base_dir}/LASV-{wildcards.segment}.treefile" {output.trees} || \
            (echo "Failed to copy {wildcards.segment} tree file" && exit 1)
        fi
        echo "Processing for segment {wildcards.segment} completed successfully."
        """

rule extract_sequences:
    output:
        "{OUTDIR}/raw/lasv-{segment}.denovo.fasta"
    input:
        contigs=config["CONTIGS_TABLE"],
        seq_data_dir=config["SEQ_DATA_DIR"],
    shell:
        """
        mkdir -p {OUTDIR}/raw
        csvtk -t filter2 -f '${{(annotation) acronym}}=="LASV" && ${{(annotation) segment}}=="{wildcards.segment}"' {input.contigs} | \
        cut -f1 | grep -v "_consensus" | \
        xargs -I @ find {input.seq_data_dir} -name "*@*" -type f -exec cat {{}} + > {output}
        """

rule align_sequences:
    output:
        aln="{OUTDIR}/lasv-{segment}.add.denovo.aln"
    input:
        sequences=rules.extract_sequences.output[0],
        base_aln=rules.prepare_base_files.output.alignments
    params:
        threads=config["MAFFT"]["THREADS"]
    shell:
        """
        mafft --add {input.sequences} --adjustdirection --thread {params.threads} {input.base_aln} > {output.aln}
        """

rule trim_alignment:
    output:
        aln="{OUTDIR}/lasv-{segment}.denovo.trimal.aln"
    input:
        rules.align_sequences.output.aln
    shell:
        """
        trimal -in {input} -out {output.aln} -automated1
        """

rule build_tree:
    output:
        tree="{OUTDIR}/iqtree-out/lasv-{segment}.denovo.trimal.treefile",
        log="{OUTDIR}/iqtree-out/lasv-{segment}.denovo.trimal.log"
    input:
        alignment=rules.trim_alignment.output.aln,
        base_tree=rules.prepare_base_files.output.trees
    params:
        model=config["IQTREE"]["MODEL"],
        bootstrap=config["IQTREE"]["BOOTSTRAPS"],
        prefix="{OUTDIR}/iqtree-out/lasv-{segment}.denovo.trimal"
    threads: workflow.cores
    shell:
        """
        mkdir -p {OUTDIR}/iqtree-out/
        iqtree -m {params.model} -B {params.bootstrap} -T {threads} -g {input.base_tree} -redo -s {input.alignment} \
        -pre {params.prefix}
        """

rule determine_duplicates:
    output:
        mldist="{OUTDIR}/dedup/lasv-{segment}.denovo.trimal.mldist",
        bad_seqs="{OUTDIR}/dedup/lasv-{segment}.bad_seqs.txt",
        good_seqs="{OUTDIR}/dedup/lasv-{segment}.good_seqs.txt"
    input:
        tree=rules.build_tree.output.tree,
        alignment=rules.trim_alignment.output.aln,
        table=config["CONTIGS_TABLE"]
    params:
        prefix="{OUTDIR}/dedup/lasv-{segment}",
        sample_regex=config["DEDUPLICATE"]["SAMPLE_REGEX"],
        threshold=config["DEDUPLICATE"]["THRESHOLD"],
        reads_column=config["DEDUPLICATE"]["READS_COLUMN"]
    run:
        determine_duplicates(
            tree=input.tree,
            alignment=input.alignment,
            prefix=params.prefix,
            table=input.table,
            sample_regex=params.sample_regex,
            threshold=params.threshold,
            reads_column=params.reads_column,
            )